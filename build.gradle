import org.apache.tools.ant.filters.ReplaceTokens
import java.nio.file.Files

plugins {
    id "com.github.jk1.dependency-license-report" version "0.3.5"
    id 'edu.sc.seis.launch4j' version '2.0.1'
}
apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'maven'
apply from: "${project.rootDir}/gradle/eclipse.gradle"


sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'de.mediathekview'
version = '13.1.0'

def jarName = 'MediathekView.jar'
def mainClass = 'mediathek.Main'

ext {
    swingxVersion = '1.6.6-SNAPSHOT'
    swingxGroup = ''
    propsFile = file('src/main/resources/version.properties').absoluteFile
    if (!propsFile.exists()) {
        Files.createFile(propsFile.toPath())
    }
}

def loadVersionProperties() {
    Properties props = new Properties()
    props.load(propsFile.newDataInputStream())
    return props
}


compileJava {
    options.compilerArgs = ['-Xlint:all']
}
compileTestJava {
    options.compilerArgs = ['-Xlint:all']
}

configurations {
    deployerJars
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://packagecloud.io/mediathekview/MediathekView/maven2"
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile 'de.mediathekview:MLib:3.0.1'

    compile "$swingxGroup:swingx-action:$swingxVersion"
    compile "$swingxGroup:swingx-autocomplete:$swingxVersion"
    compile "$swingxGroup:swingx-beaninfo:$swingxVersion"
    compile "$swingxGroup:swingx-common:$swingxVersion"
    compile "$swingxGroup:swingx-core:$swingxVersion"
    compile "$swingxGroup:swingx-graphics:$swingxVersion"
    compile "$swingxGroup:swingx-painters:$swingxVersion"
    compile "$swingxGroup:swingx-plaf:$swingxVersion"

    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.0'
    compile 'com.jidesoft:jide-oss:3.6.16'
    compile 'org.tukaani:xz:1.5'
    compile 'com.jgoodies:jgoodies-forms:1.9.0'
    compile 'net.sf.jchart2d:jchart2d:3.3.2'
    compile 'net.engio:mbassador:1.3.0'
    compile 'com.nativelibs4java:bridj:0.7.0'

    compileOnly 'com.yuvimasory:orange-extensions:1.3.0'
    deployerJars "io.packagecloud.maven.wagon:maven-packagecloud-wagon:0.0.6"
}

configurations.compile {
    resolutionStrategy.dependencySubstitution {
        def substituteIfExists = { String moduleName, String projectName ->
            if(findProject(projectName)) {
               assert !(project.hasProperty('org.gradle.configureondemand') && Boolean.valueOf(project.getProperty('org.gradle.configureondemand'))) : "org.gradle.configureondemand = true will cause problems when substituting a project"
               logger.lifecycle "$project: Substituting module '$moduleName' with project $projectName"
               substitute module(moduleName) with project(projectName)
            }
        }
        substituteIfExists('de.mediathekview:MLib', ':MLib')
    }
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        uniqueVersion=false
        def repoPw;
        
        if(project.hasProperty('repoPw'))
        {
           repoPw = project.property('repoPw');
        }else
        {
            repoPw = System.getenv('PACKAGECLOUD_TOKEN');
        }
        
        repository(url: "packagecloud+https://packagecloud.io/mediathekview/MediathekView/") {
            authentication(password:  repoPw)
        }
    }
}
build.dependsOn(install)

task updateVersion {
    doLast {
        Properties props = loadVersionProperties()

        def oldVersion = props.getProperty('VERSION')
        String buildDate = new Date().format('dd.MM.yyyy HH:mm:ss')
        if (!oldVersion.equals(project.version)) {
            logger.lifecycle "==MediathekView======================"
            logger.lifecycle "Version:  $project.version"
            logger.lifecycle "Baudatum: $buildDate"
            logger.lifecycle "==MediathekView======================"
            props.setProperty('VERSION', project.version)
            props.setProperty('DATE', buildDate)
            props.store(propsFile.newWriter(), null)
        }
    }
}

processResources.dependsOn updateVersion


licenseReport {
    outputDir = "build/Copyright/"
    excludes = [':MLib', 'MediathekView:MLib']
    configurations = ['compile']
}

processResources.dependsOn generateLicenseReport

tasks.withType(Zip) { task ->
    task.doLast {
        ant.checksum(algorithm: 'SHA-1', file: it.archivePath)
    }
}
tasks.withType(Tar) { task ->
    task.doLast {
        ant.checksum(algorithm: 'SHA-1', file: it.archivePath)
    }
}
/*
task copyProgrammIconsInRes(type: Copy) {
    logger.lifecycle 'Copying Programm icons.'
    from 'src/main/resources/mediathek/res/programm'
    into 'res/Icons/Programm/Version-' + version
    include '*.png'
    exclude 'about'
}

processResources.dependsOn copyProgrammIconsInRes

task copySenderIconsInRes(type: Copy) {
    logger.lifecycle 'Copying Sender icons.'
    from 'src/main/resources/mediathek/res/sender'
    into 'res/Icons/Sender'
    include '*.png'
    exclude '*.xcv'
}

processResources.dependsOn copySenderIconsInRes
*/

[distZip, distTar]*.shouldRunAfter compileJava, updateVersion, jar
distTar.compression = Compression.GZIP
distTar.extension = 'tar.gz'

jar {
    manifest {
        attributes(
            'SplashScreen-Image': 'mediathek/res/splash.png',
            'Main-Class': mainClass,
            'Class-Path': configurations.runtime.files.collect { "lib/$it.name" }.join(' ')
        )
    }
    archiveName = jarName
}

distributions {
    main {
        baseName = 'MediathekView'
        contents {
            into('lib') {
                from configurations.compile
            }
            from('res') {
                filesMatching('**/*.sh') {
                    filter(ReplaceTokens, tokens: [JARNAME: jarName])
                }
                filesMatching('**/*.txt') {
                    filter(ReplaceTokens, tokens: [JARNAME: jarName])
                }
                filesMatching('**/*.command') {
                    filter(ReplaceTokens, tokens: [JARNAME: jarName])
                }
            }
            from('build') {
                include 'Copyright/*'
            }
            from('build/libs') {
                include '*.jar'
            }
            from('build/launch4j') {
                include '*.exe'
                exclude 'lib'
            }
        }
    }
}

//Windows executable generation
launch4j {
    mainClassName = mainClass
    icon = "../../res/Icons/MediathekView.ico"
    jar = jarName
    dontWrapJar = true
    outfile = "MediathekView_ohne_feste_Speicherzuweisung.exe"
}

task creatMitSpeicherExe(type: edu.sc.seis.launch4j.tasks.Launch4jLibraryTask) {
    outfile = "MediathekView.exe"

    initialHeapSize = 256
    maxHeapSize = 2048
    maxHeapPercent = 80
}

task createIpv4Exe(type: edu.sc.seis.launch4j.tasks.Launch4jLibraryTask) {
    outfile = "MediathekView_ipv4.exe"
    opt = "-Djava.net.preferIPv4Stack=true"

    initialHeapSize = 256
    maxHeapSize = 2048
    maxHeapPercent = 80
}

task createPortableExe(type: edu.sc.seis.launch4j.tasks.Launch4jLibraryTask) {
    outfile = "MediathekView_Portable.exe"
    cmdLine = "Einstellungen/.mediathek3"

    initialHeapSize = 256
    maxHeapSize = 2048
    maxHeapPercent = 80
}

assemble.dependsOn createAllExecutables

/**
 * This is how you pass arguments: "./gradlew run -Pargs=arg1,arg2,arg3
 */
task run(type: JavaExec, dependsOn: classes) {
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath

    if (project.hasProperty('args')) {
        args(project.args.split(','))
    }

}

/**
 * HOWTO debug:
 *  1. run "gradle debug"
 *  2. Call your IDE to connect to a remote java application on port 5005.
 *
 * This is how you pass arguments: "./gradlew debug -Pargs=arg1,arg2,arg3
 */
task debug(type: JavaExec, dependsOn: classes) {
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath
    debug true

    if (project.hasProperty('args')) {
        args(project.args.split(','))
    }
}

/**
 * <b>You don't have to call this. Travis will do it for you if a new releass (tag) will be build!<b/>
 * Call this so: './gradlew build release -PrepoPw=[PASSWORD]'
 */
task release(dependsOn: 'uploadArchives') {
    doLast {
        println 'Released Version '+version
    }
}

/**
 * <b>You don't have to call this. Travis will do it for you if you push to develop!<b/>
 * Call this so: './gradlew build releaseSnapshot -PrepoPw=[PASSWORD]'
 */
task releaseSnapshot(dependsOn: 'uploadArchives') {
    doLast {
        println 'Released Snapshot Version '+version
    }
}

gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(releaseSnapshot)) {
        version = version+'-SNAPSHOT'
    }
}
